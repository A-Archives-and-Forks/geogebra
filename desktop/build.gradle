plugins {
    alias libs.plugins.spotbugs
    id 'application'
    id 'eclipse'
    id 'checkstyle'
}

project.setDescription('Parts of GeoGebra related to desktop platforms')

sourceSets {
    nonfree
    gpl
    main {
        resources {
            if (project.hasProperty("usegpl")) {
                srcDirs += gpl.resources.srcDirs
            } else {
                srcDirs += nonfree.resources.srcDirs
            }
        }
    }
    e2eTest {
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output
    }
}

configurations {
    e2eTestImplementation.extendsFrom testImplementation
    e2eTestImplementation.extendsFrom testRuntime
}

eclipse.classpath.sourceSets.removeAll { it.name == 'nonfree' || it.name == 'gpl' }
eclipse.classpath.file {
    // Eclipse dependecies are based on projects, not source paths
    whenMerged { classpath ->
        classpath.entries.removeAll { entry ->
            entry.kind == 'lib' && entry.path.matches(".*common-jre/build/.*/test")
        }
    }
}

dependencies {
    implementation project(':common')
    implementation project(':common-jre')
    implementation project(':editor-desktop')
    implementation project(':jogl2')
    implementation project(':giac-jni')
    implementation libs.jsObject, libs.openGeoProver, libs.jna, libs.echosvg

    implementation variantOf(libs.jogl) { classifier("natives-linux-amd64") },
            variantOf(libs.jogl) { classifier("natives-windows-amd64") },
            variantOf(libs.jogl) { classifier("natives-macosx-universal") }

    runtimeOnly variantOf(libs.gluegen.rt) { classifier("natives-linux-amd64") },
            variantOf(libs.gluegen.rt) { classifier("natives-windows-amd64") },
            variantOf(libs.gluegen.rt) { classifier("natives-macosx-universal") }

    runtimeOnly variantOf(libs.giac.java) { classifier("natives-linux-amd64") },
            variantOf(libs.giac.java) { classifier("natives-windows-amd64") },
            variantOf(libs.giac.java) { classifier("natives-macosx-universal") }

    testImplementation project(':keyboard-base')
    testImplementation project(':ggbjdk')
    testImplementation testFixtures(project(':common-jre'))
    testImplementation libs.junit, libs.mockito.core, libs.hamcrest
}

mainClassName = "org.geogebra.desktop.GeoGebra3D"

run {
    // Copying JOGL related native JARs into the same directory where the non-native JAR takes place.
    // JOGL is simply dumb, it cannot work neither with java.library.path nor classpath or anything. Arrgh.
    def joglVersion = libs.versions.jogl.get()
    def gluegen = project.configurations.runtimeClasspath.find { it.name == "gluegen-rt-${joglVersion}.jar" }
    def gluegen_natives = project.configurations.runtimeClasspath.findAll { it.name.startsWith("gluegen-rt-$joglVersion-natives") }
    def gluegen_dir = gluegen.getParent()
    for (i in gluegen_natives) {
        def gluegen_native_path = i.getPath()
        ant.copy(file: "$gluegen_native_path", todir: "$gluegen_dir")
    }
    def jogl = project.configurations.runtimeClasspath.find { it.name == "jogl-all-${joglVersion}.jar" }
    def jogl_natives = project.configurations.runtimeClasspath.findAll { it.name.startsWith("jogl-all-$joglVersion-natives") }
    def jogl_dir = jogl.getParent()
    for (i in jogl_natives) {
        def jogl_native_path = i.getPath()
        ant.copy(file: "$jogl_native_path", todir: "$jogl_dir")
    }

}
test {
    systemProperty "ggb.prerelease", project.findProperty("prerelease") ?: "true";

    ignoreFailures = true
    outputs.upToDateWhen { false }
}
spotbugs {
    ignoreFailures = true
    excludeFilter = file("../config/spotbugs.xml")
    jvmArgs = ['-Dfindbugs.sf.comment=true']
}

import org.gradle.plugins.ide.eclipse.model.AccessRule

eclipse {
    classpath {
        file {
            whenMerged { classpath ->
                def jre = entries.find { it.path.contains 'org.eclipse.jdt.launching.JRE_CONTAINER' }
                jre.accessRules.add(new AccessRule('accessible', 'com/**'))
            }
        }
    }
}

checkstyle {
    configFile file("../config/checkstyle/checkstyle.xml")
}

jar {
    manifest {
        attributes(
                'Class-Path': configurations.runtimeClasspath.collect { it.getName() }.join(' '),
                'Main-Class': 'org.geogebra.desktop.GeoGebra3D'
        )
    }
}

task debugJars(type: Zip, dependsOn: 'jar') {
    description = 'Collect all jar files in a single archive. Fast: no proguard or code signing.'
    archiveBaseName = 'jars'
    destinationDirectory = file('build')
    from 'build/libs'
    doLast {
        configurations.runtimeClasspath.each { jarFile ->
            copy {
                from jarFile
                into 'build/libs'
            }
        }
    }
}

tasks['checkstyleMain'].setSource(files([]))

task e2eTest(type: Test) {
    description 'Run end-to-end tests'
    testClassesDirs = sourceSets.e2eTest.output.classesDirs
    classpath = sourceSets.e2eTest.runtimeClasspath
}
test {
    testLogging {
        afterSuite { desc, result ->
            if (!desc.parent) { // will match the outermost suite
                println "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
            }
        }
    }
}