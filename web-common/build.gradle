plugins {
    id 'java-library'
    id 'pmd'
    id 'checkstyle'
    alias libs.plugins.gwt
    alias libs.plugins.spotbugs
}

description 'Web platform code which does not use the GWT Widgets library'

evaluationDependsOn(':common')
evaluationDependsOn(':ggbjdk')
evaluationDependsOn(':editor-web')
evaluationDependsOn(':web-dev')

dependencies {
    api project(':common'),
            project(':gwtutil'),
            project(':renderer-base'),
            project(':editor-base'),
            project(':renderer-web'),
            project(':editor-web'),
            project(':ggbjdk'),
            project(':web-dev'),
            files(project(':gwtutil').sourceSets.main.java.srcDirs),
            files(project(':common').sourceSets.main.allSource.srcDirs),
            files(project(':ggbjdk').sourceSets.main.java.srcDirs),
            files(project(':editor-web').sourceSets.main.allSource.srcDirs),
            files(project(':web-dev').sourceSets.main.allSource.srcDirs),
            files(file("build/generated/sources/annotationProcessor/java/main/")),
            files(file("build/generated/sources/webjars/java/main/")),
            'org.webjars.npm:fflate:0.8.0',
            libs.giac.gwt,
            variantOf(libs.giac.gwt) { classifier("sources") },
            libs.gwt.dev


    annotationProcessor project(":gwtutil"), libs.gwt.resources.processor
}
apply from: "../gradle-scripts/webjars.gradle"
compileJava.dependsOn('extractJs')

task extractJs() {
    doLast {
        extractWebjars()
    }
}

compileJava.options.sourcepath = files(processResources.destinationDir).builtBy(processResources)

spotbugs {
    ignoreFailures = true
    excludeFilter = file("../config/spotbugs.xml")
    jvmArgs = ['-Dfindbugs.sf.comment=true']
}

pmd {
    ignoreFailures = true
    ruleSets = []
    ruleSetFiles = files("../config/pmd.xml")
}

gwt {
    gwtVersion = libs.versions.gwt.get()

    modules = ['org.geogebra.web.WebSimple']
    devModules = modules

    maxHeapSize = '2000M'

    jsInteropExports {
        shouldGenerate = true;
    }

    compiler {
        // Customize the GWT compiler here
        strict = true
        disableCastChecking = true
        logLevel = 'TRACE'

        if (project.hasProperty("greport")) {
            compileReport = true
        }
        if (project.hasProperty("gdraft")) {
            draftCompile = true
        }
        if (project.hasProperty("gworkers")) {
            localWorkers = project.getProperty("gworkers")
        }
        if (project.hasProperty("gdetailed")) {
            style = org.docstr.gradle.plugins.gwt.Style.DETAILED
        } else {
            disableClassMetadata = true
        }
        if (project.hasProperty("gsoyc")) {
            soycDetailed = true
        }
    }
}
