plugins {
    id 'java-library'
    id 'maven-publish'
    id 'pmd-conventions'
    id 'checkstyle-conventions'
    id 'spotbugs-conventions'
    alias libs.plugins.javacc
}

group 'com.himamis.retex'
version '0.1'

publishing {
    publications {
        library(MavenPublication) {
            from components.java
        }
    }
}

dependencies {
    javacc libs.javacc
    api project(':renderer-base')
    implementation libs.j2objc.annotations
    testImplementation libs.junit
}

sourceSets.main.java.srcDirs += [compileJavacc.outputDirectory, compileJavacc.inputDirectory]

compileJavacc {
    arguments = [static                   : 'false',
                 grammar_encoding         : 'UTF-8',
                 unicode_input            : 'true',
                 JAVA_TEMPLATE_TYPE       : 'modern',
                 LEGACY_EXCEPTION_HANDLING: 'false']
}

compileJava {
    options.encoding = 'UTF-8'
}

tasks.register('cleanJavacc', Delete) {
    description 'Cleans the javacc generated files.'
    delete compileJavacc
}
