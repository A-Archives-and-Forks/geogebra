plugins {
    alias androidLibs.plugins.android.library
    id 'maven-publish'
}

group 'com.himamis.retex'
version '0.1'

publishing {
    publications {
        library(MavenPublication) {
            artifact "${project.buildDir}/outputs/aar/${project.name}-${project.version}.aar"

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                configurations.compile.allDependencies.each {
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', it.group)
                    dependencyNode.appendNode('artifactId', it.name)
                    dependencyNode.appendNode('version', it.version)
                }
            }
        }
    }
}

dependencies {
    api project(':renderer-base')
    coreLibraryDesugaring androidLibs.desugarJdkLibs
}

android {
    namespace "com.himamis.retex.renderer.android"

    compileSdk = androidLibs.versions.compileSdk.get().toInteger()

    compileOptions {
        coreLibraryDesugaringEnabled true
    }

    defaultConfig {
        targetSdk = androidLibs.versions.targetSdk.get().toInteger()
        minSdk = androidLibs.versions.minSdk.get().toInteger()
    }
}
