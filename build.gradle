description 'GeoGebra'

apply from: 'gradle-scripts/dependencies.gradle'

subprojects {
	apply plugin: 'java'

	java {
		toolchain {
			languageVersion = JavaLanguageVersion.of(11)
		}
	}

	plugins.withType(CheckstylePlugin).configureEach {
		checkstyle {
			toolVersion = '10.3.3'
		}
	}

	afterEvaluate {
		// forward command line properties to every test JVM
		tasks.withType(Test.class).each { task ->
			project.properties.each { prop, val ->
				if (val instanceof String) {
					task.systemProperty prop.trim(), val
				}
			}
		}
		tasks.matching {it.class.name.contains('SpotBugs')}.each {
			it.reports { xml.enabled = true }
		}
	}
}

// https://gist.github.com/mashimom/891a55878eda510d316e
if (JavaVersion.current().isJava8Compatible()) {
  allprojects {
    tasks.withType(Javadoc).configureEach {
      options.addStringOption('Xdoclint:none', '-quiet')
	  options.addStringOption('tag', 'example:X')
    }
  }
}
